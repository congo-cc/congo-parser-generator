DEFAULT_LEXICAL_STATE=CSHARP;
PARSER_PACKAGE=org.parsers.csharplexer;
EXTRA_TOKENS=PP_IF,PP_ENDIF,PP_ELSE,PP_ELIF,PP_DEFINE,PP_UNDEF;

<CSHARP> SKIP :
  <WHITESPACE : (<WS_CHAR>)+> #Whitespace
  |
  <#WS_CHAR :
       [
         " ", "\t", "\n", "\r",
         "\u000b", "\u00a0", "\u1680",
         "\u2000"-"\u200a",
         "\u202f", "\u205f", "\u3000"
       ]
  >
  |
  <#NEW_LINE : ["\n", "\r", "\u0085", "\u2028", "\u2029"]>
  |
  <#NOT_NEW_LINE : ~["\n", "\r", "\u0085", "\u2028", "\u2029"]>
;

UNPARSED #Comment :
  < ?MULTI_LINE_COMMENT : "/*" (~[])* "*/">
  |
  < SINGLE_LINE_COMMENT : "//" (<NOT_NEW_LINE>)* <NEW_LINE>>
;

TOKEN #Literal :
  < INTEGER_LITERAL: (<DECIMAL_LITERAL> | <HEX_LITERAL> | <BINARY_LITERAL>) (<INTEGER_TYPE_SUFFIX>)?> #IntegerLiteral
  |
  < REAL_LITERAL:
     ((<DECIMAL_LITERAL>)? ".")? <DECIMAL_LITERAL> (<EXPONENT_PART>)? (["f","F","d","D","m","M"])?
  > #RealLiteral
  |
  < #DECIMAL_LITERAL: ["0"-"9"]((["0"-"9","_"])*["0"-"9"])?>
  |
  < #HEX_LITERAL: "0" ["x","X"] <HEX_DIGITS>>
  |
  < #BINARY_LITERAL : "0b" (["0", "1", "_"])+ (["0","1"])?>
  |
  < #INTEGER_TYPE_SUFFIX : "U" | "u" | "L" | "l" | "UL" | "Ul" | "uL" | "ul"| "LU" | "Lu" | "lU" | "lu">
  |
  < #HEX_DIGITS: (["0"-"9","a"-"f","A"-"F","_"])* ["0"-"9","a"-"f","A"-"F"] >
  |
  < #EXPONENT_PART : ["e","E"] (["+","-"])? <DECIMAL_LITERAL> >
  |
  < FALSE : "false" > #BooleanLiteral
  |
  < TRUE : "true"> #BooleanLiteral
  |
  < NULL : "null" > #NullLiteral
;

TOKEN #Delimiter :
  < LPAREN: "(" >
| < RPAREN: ")" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
;

TOKEN #Operator :
 < GT: ">" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
// The following two since 8.0
| < DOUBLE_HOOK: "??">
| < DOUBLE_HOOK_EQUALS : "??=">
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < HAT: "^" >
| < REM: "%" >
| < LEFT_SHIFT: "<<" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LEFT_SHIFT_ASSIGN: "<<=" >
| < RIGHT_SHIFT: ">>" >
| < UNSIGNED_RIGHT_SHIFT: ">>>" >
| < RIGHT_SHIFT_ASSIGN: ">>=" >
| < UNSIGNED_RIGHT_SHIFT_ASSIGN: ">>>=" >
| < ARROW : "=>">
| < POINTER_ACCESS : "->">
| < ASSIGN: "=" >
| < DOUBLE_COLON: "::" >
| < DOT: "." >
| < RANGE : ".." >
| < VAR_ARGS: "..." >
| < AT : "@">
;

// C# Keywords

TOKEN #KeyWord :
   <ABSTRACT : "abstract" >
   | < AS : "as" >
   | < BASE : "base" >
   | < BOOL : "bool" > #PrimitiveType
   | < BREAK : "break" >
   | < BYTE : "byte" > #PrimitiveType
   |  <CASE : "case" >
   | < CATCH : "catch" >
   | < CHAR : "char" > #PrimitiveType
   | < CHECKED : "checked" >
   | < CLASS : "class" >
   | < CONST : "const" >
   | < CONTINUE : "continue" >
   | < DECIMAL : "decimal" > #PrimitiveType
   | < DEFAULT : "default" >
   | < DELEGATE : "delegate" >
   | < DO : "do" >
   | < DOUBLE : "double" > #PrimitiveType
   | < ELSE : "else" >
   | < ENUM : "enum" >
   | < EVENT : "event" >
   | < EXPLICIT : "explicit" >
   | < EXTERN : "extern" >
   | < FINALLY : "finally" >
   | < FIXED : "fixed" >
   | < FLOAT : "float" > #PrimitiveType
   | < FOR : "for" >
   | < FOREACH : "foreach" >
   | < GOTO : "goto" >
   | < IF : "if" >
   | < IMPLICIT : "implicit" >
   | < IN : "in" >
   | < INT : "int" > #PrimitiveType
   | < INTERFACE : "interface" >
   | < INTERNAL : "internal" >
   | < IS : "is">
   | < LOCK : "lock" >
   | < LONG : "long" > #PrimitiveType
   | < NAMESPACE : "namespace" >
   | < NEW : "new" >
   | < OBJECT : "object" >
   | < OPERATOR : "operator" >
   | < OUT : "out" >
   | < OVERRIDE : "override" >
   | < PARAMS : "params">
   | < PRIVATE : "private" >
   | < PROTECTED : "protected" >
   | < PUBLIC : "public" >
   | < READONLY : "readonly" >
   | < REF : "ref" >
   | < RETURN : "return" >
   | < SBYTE : "sbyte" > #PrimitiveType
   | < SEALED : "sealed" >
   | < SHORT : "short" > #PrimitiveType
   | < SIZEOF : "sizeof" >
   | < STACKALLOC : "stackalloc" >
   | < STATIC : "static" >
   | < STRING : "string" > #PrimitiveType
   | < STRUCT : "struct" >
   | < SWITCH : "switch" >
   | < THIS : "this" >
   | < THROW : "throw" >
   | < TRY : "try" >
   | < TYPEOF : "typeof" >
   | < UINT : "uint" > #PrimitiveType
   | < ULONG : "ulong" > #PrimitiveType
   | < UNCHECKED : "unchecked" >
   | < UNSAFE : "unsafe" >
   | < USHORT : "ushort" > #PrimitiveType
   | < USING : ("global" <WHITESPACE>)? "using" >
   | < VIRTUAL : "virtual" >
   | < VOID : "void" >
   | < VOLATILE : "volatile" >
   | < WHILE : "while" >
// Now contextual Keywords
   | < ARGLIST : "__arglist">
   | < ADD : "add">
   | < ASYNC : "async" >
//   | < AWAIT : "await" >
   | < DYNAMIC : "dynamic">
   | < GET : "get" >
   | < FILE : "file" > // Since C# 11
   | < INIT : "init" >
   | < NAMEOF : "nameof">
   | < PARTIAL : "partial">
//   | < RECORD : "record" >
//   | < REMOVE : "remove">
   | < REQUIRED : "required">
   | < SCOPED : "scoped">
   | < SET : "set" >
   | < VAR : "var" >
   | < AND : "and">
   | < OR  : "or" >
   | < NOT : "not">
// Here are the soft keywords used in queries
   | <ASCENDING : "ascending">
   | <BY : "by">
   | <DESCENDING : "descending">
   | <EQUALS : "equals">
   | <FROM : "from">
   | <GROUP : "group">
   | <INTO : "into">
   | <JOIN : "join">
   | <LET : "let">
   | <ON : "on">
   | <ORDERBY : "orderby" >
   | <SELECT : "select">
   | <WHERE : "where">
;

INCLUDE "CSharpIdentifierDef.ccc"

TOKEN :
  <IDENTIFIER : ("@")? (<CSHARP_IDENTIFIER_START>|<UNICODE_ESCAPE>) (<CSHARP_IDENTIFIER_PART>|<UNICODE_ESCAPE>)*> #Identifier
  |
  <#UNICODE_ESCAPE : "\\"
                    (
                      "u" (["0"-"9", "a"-"f", "A"-"F"]){4}
                      |
                      "U" (["0"-"9", "a"-"f", "A"-"F"]){8}
                    ) >
;

TOKEN #Literal :
  < CHARACTER_LITERAL: "'" ((~["'","\\","\n","\r", "\u0085", "\u2028", "\u2029"]) | <ESCAPED_CHAR>) "'"> #CharacterLiteral
  |
  < #ESCAPED_CHAR :  "\\"
                    (
                      (["0","a","b","f","n", "r","t","v","\\","'",'"'])
                      |
                      "x" (["0"-"9", "a"-"f", "A"-"F"]){1,4}
                      |
                      "u" (["0"-"9", "a"-"f", "A"-"F"]){4}
                    )
  >
  |
  < #ESCAPED_CHAR2 :  "\\"
                    (
                      (["0","a","b","f","n", "r","t","v","\\","'",'"'])
                      |
                      "x" (["0"-"9", "a"-"f", "A"-"F"]){1,4}
                      |
                      "u" (["0"-"9", "a"-"f", "A"-"F"]){4}
                      |
                      "U" (["0"-"9", "a"-"f", "A"-"F"]){8}
                    )
  >
  |
  < REGULAR_STRING_LITERAL : '"' ((~['"',"\\","\n","\r","\u0085", "\u2028", "\u2029"]) | <ESCAPED_CHAR2>)* '"' ("u8")?> #StringLiteral
  |
  < VERBATIM_STRING_LITERAL : '@"' (~['"'] | '""')* '"' > #StringLiteral
  |
  < REGULAR_INTERPOLATION_START : '$"' >
  |
  < MULTI_INTERPOLATION_START : '$@"' | '@$"' >
  |
  < INTERPOLATED_RAW_STRING_START : ("$")+ ('"'){3,}>
;

<IN_REGULAR_INTERPOLATION>
TOKEN :
  // Just any non-interpolated character in an interpolated string.
  < NON_INTERPOLATED_TEXT : ((~['"',"\\","\n","\r","\u0085", "\u2028", "\u2029","{","}"]) | "{{" | "}}" | <ESCAPED_CHAR2>)+ >
;

<IN_MULTI_INTERPOLATION>
TOKEN :
  // Just any non-interpolated character in an interpolated string.
  < NON_INTERPOLATED_TEXT2 : ((~['"',"{","}"] | '""') | "{{" | "}}")+ >
;

<CSHARP, IN_REGULAR_INTERPOLATION, IN_MULTI_INTERPOLATION >
TOKEN #Delimiter :
   <LBRACE : "{" >
;

<IN_REGULAR_INTERPOLATION,IN_MULTI_INTERPOLATION >
TOKEN :
  < ENDING_QUOTE : '"' >
;

//<IN_IGNORED_SECTION> SKIP : <  ~[] > ;

<CSHARP, IN_IGNORED_SECTION> UNPARSED :
   // <HASH : "#">
   <PP_LINE : (<HORIZONTAL_WHITESPACE>)* "#" (<NOT_NEW_LINE>)* <NEW_LINE>> #PPLine {
      String trimmed = matchedToken.toString().trim().substring(1).trim();
      if (trimmed.startsWith("if")) {
         matchedToken.setType(TokenType.PP_IF);
      } else if (trimmed.startsWith("elif")) {
         matchedToken.setType(TokenType.PP_ELIF);
      } else if (trimmed.startsWith("else")) {
         matchedToken.setType(TokenType.PP_ELSE);
      } else if (trimmed.startsWith("endif")) {
         matchedToken.setType(TokenType.PP_ENDIF);
      } else if (trimmed.startsWith("define")) {
         matchedToken.setType(TokenType.PP_DEFINE);
      } else if (trimmed.startsWith("undef")) {
         matchedToken.setType(TokenType.PP_UNDEF);
      }
   }
;

<IN_IGNORED_SECTION> UNPARSED :
    <#HORIZONTAL_WHITESPACE :
       [
         " ", "\t",
         "\u000b", "\u00a0", "\u1680",
         "\u2000"-"\u200a",
         "\u202f", "\u205f", "\u3000"
       ]
   >
   |
   <WHITESPACE_LINE : (<HORIZONTAL_WHITESPACE>)* <NEW_LINE>>
   |
   <IGNORED_LINE : (<HORIZONTAL_WHITESPACE>)*
                    ~["#", " ", "\t", "\u000b", "\u00a0", "\u1680", "\u2000"-"\u200a",
                      "\u202f", "\u205f", "\u3000", "\n", "\r", "\u0085", "\u2028", "\u2029"]
                    (<NOT_NEW_LINE>)*
                    <NEW_LINE>
   >
;


MORE :
   <RAW_STRING_START : ('"'){3,}> : IN_RAW_STRING
;


<IN_INTERPOLATED_RAW_STRING> TOKEN :
   <START_INTERPOLATION : ("{")+>
   |
   <NON_INTERPOLATED_TEXT3 : (~['{', '}', '"'])+ | ('"' ('"')?) >
   |
   <END_RAW_INTERPOLATION : ("}")+>
   |
   <INTERPOLATED_RAW_STRING_END : ('"'){3,} >
;

<IN_RAW_STRING> MORE :
   <ANY_CHAR : ~[]>
;

<IN_RAW_STRING> TOKEN :
    <RAW_STRING_LITERAL : ('"'){3,}("u8")?> #StringLiteral
;

INJECT LEXER_CLASS :
{
   private String pendingString = "";
   private int startingQuotes, startingDollars;

   private BASE_TOKEN_CLASS TOKEN_HOOK(BASE_TOKEN_CLASS tok) {
      TokenType type = tok.getType();
      if (type == TokenType.INTERPOLATED_RAW_STRING_START) {
         String image = tok.toString();
         startingQuotes = startingDollars = 0;
         for (int i = 0; i < image.length(); i++) {
             char ch = image.charAt(i);
             if (ch == '"') startingQuotes++;
             else if (ch == '$') startingDollars++;
             else break;
         }
      }
      else if (type == TokenType.INTERPOLATED_RAW_STRING_END) {
        if (tok.toString().length() < startingQuotes) {
            tok.setType(TokenType.NON_INTERPOLATED_TEXT3);
        }
        return tok;
      }
      else if (type == TokenType.NON_INTERPOLATED_TEXT3) {
         int followingOpenBraces = 0;
         for (int k = tok.getEndOffset(); k < this.length(); k++) {
//            if (!isIgnored(k)) {
                if (this.charAt(k) == '{') followingOpenBraces++;
                else break;
//            }
         }
         int amountToExtend = followingOpenBraces - startingDollars;
         if (amountToExtend < 0) amountToExtend = followingOpenBraces;
         tok.setEndOffset(tok.getEndOffset() + amountToExtend);
      }
      else if (type == TokenType.START_INTERPOLATION) {
         int tokLength = tok.toString().length();
         if (tokLength > startingDollars) {
            tok.setEndOffset(tok.getEndOffset() - (tokLength-startingDollars));
            tok.setType(TokenType.NON_INTERPOLATED_TEXT3);
            return tok;
         }
         if (tok.toString().length() < startingDollars) {
            tok.setType(TokenType.NON_INTERPOLATED_TEXT3);
            return tok;
         }
         return tok;
      }
      else if (type == TokenType.END_RAW_INTERPOLATION) {
          if (tok.toString().length() < startingDollars) {
              tok.setType(TokenType.NON_INTERPOLATED_TEXT3);
              return tok;
          }
          return tok;
      }
      else
      if (tok.getType() == TokenType.RAW_STRING_LITERAL) {
        String image = tok.toString();
        if (pendingString == "" && endsWithEnoughQuotes(image)) {
          switchTo(LexicalState.CSHARP);
          return tok;
        }
        // The PythonTranslator does not seem to handle the following line
        //pendingString += tok.toString();
        pendingString = pendingString + tok.toString();
        if (endsWithEnoughQuotes(pendingString)) {
            tok.setBeginOffset(tok.getEndOffset()-pendingString.length());
            pendingString = "";
            switchTo(LexicalState.CSHARP);
            return tok;
        }
        tok.setUnparsed(true);
      }
      return tok;
   }

   private boolean endsWithEnoughQuotes(String img) {
       int startingQuotes = 0, endingQuotes = 0;
       for (int i =0; i<img.length();i++) {
          if (img.charAt(i) == '"') startingQuotes++;
          else break;
       }
       if (img.endsWith("u8")) img = img.substring(0, img.length() -2);
          for (int k = img.length() -1; k>=0; k--) {
             if (img.charAt(k) == '"') ++endingQuotes;
             else break;
          }
       return endingQuotes >=startingQuotes;
   }
}

INJECT LEXER_CLASS :
    import java.util.HashSet;
    import java.util.Set;
    import NODE_PACKAGE.PPLine;
    import org.parsers.csharp.ppline.PPDirectiveLineParser;
    import org.parsers.csharp.ppline.PPDirectiveLineParser.DirectiveType;
{
   private boolean parsingOn=true, alreadyHandled, seenElse;
   private int ifNesting;

   private Set<String> ppSymbols = new HashSet<String>();
   private Map<Integer,PPDirectiveLineParser.Info> ppInfoCache = new HashMap<Integer,PPDirectiveLineParser.Info>();

   private PPDirectiveLineParser.Info findMatchingIfInfo(BASE_TOKEN_CLASS tok) {
      Iterator<BASE_TOKEN_CLASS> precedingTokens = tok.precedingTokens();
      int nesting = 0;
      while (precedingTokens.hasNext()) {
         BASE_TOKEN_CLASS prev = precedingTokens.next();
         TokenType type = prev.getType();
         if (type == TokenType.PP_ENDIF) ++nesting;
         else if (type == TokenType.PP_IF) {
            if (nesting == 0) return ppInfoCache.get(prev.getBeginOffset());
            --nesting;
         }
      }
      return null;
   }

   void addSymbols(Set<String> symbols) {
      ppSymbols.addAll(symbols);
   }

// Token hook to handle preprocessor directives.

   private BASE_TOKEN_CLASS TOKEN_HOOK(BASE_TOKEN_CLASS tok) {
      TokenType type = tok.getType();
      if (type == TokenType.EOF && ifNesting != 0) {
         tok.setType(TokenType.INVALID);
         return tok;
      }
      if (!(tok instanceof PPLine)) return tok;
      if (!atLineStart(tok)) {
         tok.setType(TokenType.INVALID);
         tok.setUnparsed(false);
         return tok;
      }
      // We let this little helper parser defined in PPDirectiveLine.ccc
      // parse the line. So we just pass it the line where the token occurs
      // and get back an info object that tells us what we need to know.
      int beginOffset = tok.getBeginOffset();
      PPDirectiveLineParser.Info info;
      if (ppInfoCache.containsKey(beginOffset))
         info = ppInfoCache.get(beginOffset);
      else {
         info = PPDirectiveLineParser.evaluate(tok.toString(), ppSymbols, ifNesting, parsingOn, alreadyHandled, seenElse);
         ppInfoCache.put(beginOffset, info);
      }
      DirectiveType directiveType = info.directiveType;
      if (tok.getType() == TokenType.PP_ENDIF) {
         info = findMatchingIfInfo(tok);
      }
      ifNesting = info.ifNesting;
      alreadyHandled = info.alreadyHandled;
      seenElse = info.seenElse;
      ppSymbols = info.ppSymbols;
      _setParsingEnabled(info.parsingOn);
      if (directiveType == DirectiveType.UNKNOWN) {
         tok.setUnparsed(false);
         tok.setType(TokenType.INVALID);
         return tok;
      }
      if ((ifNesting == 0 || seenElse)
           && (directiveType == DirectiveType.ELIF
               || directiveType == DirectiveType.ELSE)) {
          tok.setType(TokenType.INVALID);
          tok.setUnparsed(false);
          return tok;
      }
      if (type == TokenType.PP_ELSE) {
        seenElse = true;
        _setParsingEnabled(!alreadyHandled && findMatchingIfInfo(tok).parsingOn);
      }
      else if (type == TokenType.PP_ELIF) {
        _setParsingEnabled(!alreadyHandled && info.evaluationResult && findMatchingIfInfo(tok).parsingOn);
        alreadyHandled |= info.evaluationResult;
      }
      else if (type == TokenType.PP_IF) {
        ++ifNesting;
        seenElse = false;
        alreadyHandled = info.evaluationResult;
        _setParsingEnabled(parsingOn && info.evaluationResult);
      }
      return tok;
   }

   void _setParsingEnabled(boolean parsingOn) {
       if (parsingOn) {
         switchTo(LexicalState.CSHARP);
       } else {
         switchTo(LexicalState.IN_IGNORED_SECTION);
       }
       this.parsingOn = parsingOn;
   }
}

