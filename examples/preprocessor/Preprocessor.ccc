/*
 * Authors: Jonathan Revusky revusky@congocc.org
 *          Vinay Sajip vinay_sajip@yahoo.co.uk - adapted to allow string values for symbols
 * This barebones Preprocessor is free to use or adapt.
 */

DEFAULT_LEXICAL_STATE = PP_SCAN_LINES;
TREE_BUILDING_ENABLED=false;
ENSURE_FINAL_EOL;
USES_PREPROCESSOR;
#if localtest
PARSER_PACKAGE=org.parsers.preprocessor;
#else
PARSER_PACKAGE=org.congocc.preprocessor;
BASE_SRC_DIR="../../build/generated-java";
#endif

/*
 * This is the preprocessor used internally in CongoCC. It began with the intent
 * of being an implementation of the standard C# preprocessor. However, I (JR) had to
 * re-implement the C# preprocessor for the C# grammar because I had misread the spec
 * somewhat. Oh well. It is the same basic approach. Really the only thing that is supported
 * is turning on/off parts of the source file.
 * This implementation is very bare-bones and bloody-minded. It does not build a
 * tree or any of that. It just uses code actions to create a BitSet
 * that holds whether a line is "turned on" or not. The line offsets
 * are 1-based.
 *
 * All the naming in this file is prefixed with PP_, sort of using the
 * poor man's namespace functionality. This is because I anticipate
 * this getting INCLUDEd and I don't want to stomp on other people's
 * names. It is not terribly optimal and really quite ugly. Something
 * should eventually be done about this.
 */

INJECT PARSER_CLASS :
{
    private final BitSet lineMarkers = new BitSet(100000);
    private Map<String, String> definedSymbols = new HashMap<String, String>();
    private boolean currentlyOn = true;

    public BitSet getLineMarkers() {
        return lineMarkers;
    }

    private void setLineMarker() {
        int lineNumber = lastConsumedToken.getBeginLine();
        if (currentlyOn) lineMarkers.set(lineNumber);
    }

    public void setSymbol(String symbol, String value) {
        definedSymbols.put(symbol, value);
    }

    public void unsetSymbol(String symbol) {
        definedSymbols.put(symbol, "0");
    }

    public void addSymbols(Map<String, String> symbols) {
        definedSymbols.putAll(symbols);
    }

    private static class Expression {
        boolean booleanValue;
        String symbol;
    }
}

TOKEN :
 <#PP_WS : [" ", "\t", "\f",
         "\u000b", "\u00a0", "\u1680",
         "\u2000"-"\u200a",
         "\u202f", "\u205f", "\u3000","\uC2A0"]>
 |
 <#PP_EOL : "\n" | "\r" | "\r\n" >
 |
 <PP_LINE : (<PP_WS>)* ((~["#", " ", "\t", "\f", "\n", "\r"]) (~["\n", "\r"])*)? <PP_EOL>>
 |
 <PP_DEFINE : <PP_START> "define" (<PP_WS>)*> : PP_DIRECTIVE
 |
 <PP_UNDEF : <PP_START> "undef" (<PP_WS>)*> : PP_DIRECTIVE
 |
 <PP_IF : <PP_START> "if" (<PP_WS>)*> : PP_DIRECTIVE
 |
 <PP_ELIF : <PP_START> "elif" (<PP_WS>)*> : PP_DIRECTIVE
 |
 <PP_ELSE : <PP_START> "else"> : PP_DIRECTIVE
 |
 <PP_ENDIF : <PP_START> "endif"> : PP_DIRECTIVE
;

UNPARSED :
 <PP_IGNORED_DIRECTIVE :
     <PP_START>
// Just ignore all these for now.
     ("pragma" | "line" | "warning" | "error" | "region" | "endregion" | "nullable")
     (~["\n", "\r"])*
     <PP_EOL>>
;


MORE :
//Anything else that looks like it starts a directive,
// we just treat (rightly or wrongly) as a line of code
// that is NOT ignored.
  <PP_START : (<PP_WS>)* "#" (<PP_WS>)*> : PP_EAT_TO_EOL
;

<PP_EAT_TO_EOL> TOKEN :
  <PP_FALSE_ALERT : ((~["\n", "\r"])*)? <PP_EOL>> : PP_SCAN_LINES
;

INCLUDE "JavaIdentifierDef.ccc"

<PP_DIRECTIVE> TOKEN :
    <PP_TRUE : "true">
    |
    <PP_FALSE : "false">
    |
    <PP_SYMBOL : <JAVA_IDENTIFIER_START> (<JAVA_IDENTIFIER_PART>)* >
    |
    <PP_OR : "||">
    |
    <PP_AND : "&&">
    |
    <PP_LPAREN : "(">
    |
    <PP_RPAREN : ")">
    |
    <PP_ASSIGN : "=">
    |
    <PP_EQUALS : "==">
    |
    <PP_NOT_EQUALS : "!=">
    |
    <PP_NOT : "!">
;

<PP_DIRECTIVE> UNPARSED :
    <PP_EOL_COMMENT : ("//" (~["\n", "\r"])*)? <PP_EOL>> : PP_SCAN_LINES
;

<PP_DIRECTIVE> SKIP : <(<PP_WS>)+> ;

BitSet PP_Root#void : PP_Block <EOF> {return lineMarkers;};

PP_Block#void : (PP_IfBlock | PP_DefStatement | PP_UndefStatement | PP_Content)* ;

PP_Content#void : ((<PP_LINE>|<PP_FALSE_ALERT>) {setLineMarker();})+ ;

PP_IfBlock#void :
   {
      boolean alreadyHandled = false;
      Expression result;
      final boolean previouslyOn = currentlyOn;
   }
    <PP_IF>
    result = PP_Expression {
        if (result.booleanValue) alreadyHandled = true;
        currentlyOn = result.booleanValue && previouslyOn;
    }
    PP_Block {if (alreadyHandled) currentlyOn = false;}
    (
        <PP_ELIF> result=PP_Expression
        {
            if (!alreadyHandled) {
                alreadyHandled = result.booleanValue;
                currentlyOn = previouslyOn && result.booleanValue;
            }
        }
        PP_Block
    )*
    [
        <PP_ELSE> {currentlyOn = !alreadyHandled && previouslyOn;}
        PP_Block
    ]
    <PP_ENDIF>
    {this.currentlyOn = previouslyOn;}
;

PP_DefStatement#void :
{
    String value = "1";
}
  <PP_DEFINE> <PP_SYMBOL>
  [
    <PP_ASSIGN>
    <PP_SYMBOL> {
      value = lastConsumedToken.toString();
    }
  ]
  {
    if (currentlyOn) {
      definedSymbols.put(lastConsumedToken.toString(), value);
    }
  }
;

PP_UndefStatement#void :
    <PP_UNDEF> <PP_SYMBOL>
    {if (currentlyOn) definedSymbols.remove(lastConsumedToken.toString());}
;

Expression PP_Expression#void :
  {
    Expression result, result2;
    boolean inequality = false;
  }
  result=PP_OrExpression
  [
    (<PP_EQUALS>|<PP_NOT_EQUALS>{inequality=true;})
    result2=PP_OrExpression
    {
      if (result.symbol == null || result2.symbol == null) {
        result.booleanValue = inequality == (result.booleanValue != result2.booleanValue);
      }
      else {
        String key = result.symbol;
        String value = result2.symbol;

        result.symbol = null;   // don't propagate further
        result2.symbol = null;
        if (!definedSymbols.containsKey(key)) {
          if (!definedSymbols.containsKey(value)) {
            // neither LHS nor RHS appears to be defined, so both are
            // logically false, so ...
            result.booleanValue = !inequality;
          }
          else {
            // LHS isn't defined, so logically false. RHS is defined,
            // but its value might be "0" because it was undef'ed.
            result.booleanValue = inequality != definedSymbols.get(value).equals("0");
          }
        }
        else {
          String keyValue = definedSymbols.get(key);
          result.booleanValue = inequality != value.equals(keyValue);
        }
      }
    }
  ]
  {return result;}
;

Expression PP_OrExpression#void :
  { Expression result, result2; }
   result=PP_AndExpression
  (<PP_OR> result2 = PP_AndExpression { result.booleanValue = result.booleanValue || result2.booleanValue; })*
   {return result;}
;

Expression PP_AndExpression#void :
  { Expression result, result2; }
   result=PP_NotExpression
  (<PP_AND> result2 = PP_NotExpression { result.booleanValue = result.booleanValue && result2.booleanValue; })*
   {return result;}
;

Expression PP_NotExpression#void :
  {
    boolean notFlag = false;
    Expression result;
  }
   (<PP_NOT> {notFlag = !notFlag;})*
   result=PP_PrimaryExpression
  {
     if (notFlag) {
       result.booleanValue = !result.booleanValue;
     }
     return result;
  }
;

Expression PP_Parentheses#void :
   { Expression result; }
   <PP_LPAREN>
   result=PP_Expression
   <PP_RPAREN>
   {return result;}
;

Expression PP_PrimaryExpression#void :
    { Expression result = new Expression(); }
    (
      result=PP_Parentheses
      |
      <PP_SYMBOL>
      {
        result.symbol = lastConsumedToken.toString();
        result.booleanValue = definedSymbols.containsKey(result.symbol);
      }
      |
      <PP_TRUE> { result.booleanValue = true; }
      |
      <PP_FALSE> { result.booleanValue = false; }
    )
    {return result;}
;
