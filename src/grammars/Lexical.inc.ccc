// An included part of the CongoCC grammar that is just the
// part that involves the lexical specification

TokenProduction :
{
    Token t = null;
    String tokenClassName = null;
    String firstSpecifiedLexicalState = null;
    boolean multipleStatesSpecified = false;
    boolean ignoreCase = false;
}
  [
    "<" "*" =>|| ">" {multipleStatesSpecified = true;}
    |
    "<" <IDENTIFIER>
      {
          firstSpecifiedLexicalState = lastConsumedToken.toString();
          grammar.addLexicalState(firstSpecifiedLexicalState);
      }
      ("," <IDENTIFIER> {multipleStatesSpecified = true; grammar.addLexicalState(lastConsumedToken.toString());})*
     ">"
  ]
  (<_TOKEN> | <_UNPARSED> | <_SKIP> | <_MORE> | <_CONTEXTUAL_KEYWORD>)
  [
    "[" "IGNORE_CASE" {ignoreCase = true;} "]"
  ]
  ["#" Name {
    tokenClassName=peekNode().toString();
    CURRENT_NODE.setTokenClassName(tokenClassName);
    }
  ]
  ":"
   RegexpSpec(tokenClassName)
   ( "|" RegexpSpec(tokenClassName) )*
   ";"
   {
      if (!multipleStatesSpecified) {
          if (firstSpecifiedLexicalState == null) {
              firstSpecifiedLexicalState = grammar.getDefaultLexicalState();
          }
          CURRENT_NODE.setImplicitLexicalState(firstSpecifiedLexicalState);
      }
      CURRENT_NODE.setIgnoreCase(ignoreCase || grammar.getAppSettings().isIgnoreCase());
   }
;

INJECT TokenProduction :
    import java.util.*;
    import org.congocc.core.RegexpSpec;
{
    @Property private boolean ignoreCase;
    @Property String tokenClassName;

    private String implicitLexicalState;

    public void setImplicitLexicalState(String implicitLexicalState) {
        this.implicitLexicalState = implicitLexicalState;
    }

    public List<RegexpSpec> getRegexpSpecs() {
        return childrenOfType(RegexpSpec.class);
    }

    /**
     * The states in which this regular expression production exists.
     */
    public String[] getLexicalStateNames() {

        if (implicitLexicalState != null) {
            return new String[] {implicitLexicalState};
        }
        if (isInAllLexStates()) {
             return getGrammar().getLexicalStates();
        }
        Set<String> states = new LinkedHashSet<String>();
        for (Token t : childrenOfType(Token.class)) {
            if (t.getType() == HASH) break;
            if (t.getType() == IDENTIFIER) {
                states.add(t.toString());
            }
        }
        if (states.isEmpty()) {
           return new String[] {getGrammar().getDefaultLexicalState()};
        }
        String[] lexStates = new String[states.size()];
        int i=0;
        for (String s : states) {
           lexStates[i++] = s;
        }
        return lexStates;
    }

    private boolean isInAllLexStates() {
       for (Node n : children()) {
          if (n instanceof Token) {
          Token t = (Token) n;
              if (t.getType() == STAR) {
                  return true;
              }
          }
       }
       return false;
    }

    public String getKind() {
       for (Token t : childrenOfType(Token.class)) {
           switch (t.getType()) {
              case _MORE : return "MORE";
              case _UNPARSED : return "UNPARSED";
              case _SKIP : return "SKIP";
              case _TOKEN : return "TOKEN";
              default:
           }
      }
      return "TOKEN";
    }
}

RegexpSpec(String tokenClassName)#org.congocc.core.RegexpSpec :
{
    RegularExpression regexp;
    boolean isPrivate = false;
    String image = "";
}
    (
        RegexpStringLiteral
        {   regexp = (RegularExpression) peekNode();
            ((RegexpStringLiteral) regexp).setLexicalState(getGrammar().getDefaultLexicalState());
        }
        |
        <LT>
        [
            ["?" | "#" {isPrivate = true;}]
            <IDENTIFIER> {image = lastConsumedToken.toString();}
            ":"
        ]
        RegexpChoice
        {
            regexp = (RegularExpression) peekNode();
            if (regexp instanceof RegexpRef) {
                RegexpSequence seq = new RegexpSequence();
                seq.add(regexp);
                pokeNode(seq);
                regexp = seq;
            }
            regexp.setLabel(image);
            regexp.setGeneratedClassName(image);
            regexp.setPrivate(isPrivate);
        }
        <GT>
    )
    {
        if (tokenClassName != null) {
            regexp.setGeneratedClassName(tokenClassName);
        }
    }
    [
        "#" Name
        {
            regexp.setGeneratedSuperClassName(tokenClassName);
            regexp.setGeneratedClassName(peekNode().toString());
        }
    ]
    [
        SCAN 1 {!regexp.isPrivate()} =>
        Block
    ]
    [
        SCAN 1 {!regexp.isPrivate()} =>
        ":" <IDENTIFIER> {CURRENT_NODE.setNextLexicalState(lastConsumedToken.toString());}
    ]
;


RegexpStringLiteral# :
   (
    <STRING_LITERAL>
    |<CHARACTER_LITERAL> {CURRENT_NODE.setSingleQuoted(true);}
    |<SINGLE_QUOTE_STRING> {CURRENT_NODE.setSingleQuoted(true);}
   )
   {
      String image = StringLiteral.removeEscapesAndQuotes(lastConsumedToken.toString());
      CURRENT_NODE.setLiteralString(image);
   }
;

INJECT RegexpStringLiteral :
   import org.congocc.core.*;
   extends RegularExpression;
{
  @Property String literalString, lexicalState;
  @Property boolean singleQuoted;
  @Property RegexpStringLiteral canonicalRegexp;

  public String toString() {
    return super.toString() + " - " + getLiteralString();
  }

  public int getOrdinal() {
      if (canonicalRegexp != null) {
        return canonicalRegexp.getOrdinal();
      }
      return super.getOrdinal();
  }

  public String getLabel() {
     if (canonicalRegexp != null) {
        return canonicalRegexp.getLabel();
     }
     return super.getLabel();
  }

  public int hashCode() {
      return literalString.hashCode();
  }

  @Override
  public boolean matchesEmptyString() {
      return literalString.length() == 0;
  }

  public String[] getLexicalStateNames() {
     TokenProduction tp = getTokenProduction();
     return tp != null ? tp.getLexicalStateNames() : new String[]{lexicalState};
  }

  @Override
  public int maximumLength() {
     return literalString.codePointCount(0, literalString.length());
  }
}

#RegexpRef :
{
   Token t;
}
    "<"
    t=<IDENTIFIER> =>||
    DEACTIVATE_TOKENS RSIGNEDSHIFT, RUNSIGNEDSHIFT (">")
    {
       CURRENT_NODE.setLabel(t.toString());
       return CURRENT_NODE;
    }
;

INJECT RegexpRef :
    import org.congocc.core.*;
    extends RegularExpression;
{
    @Property RegularExpression regexp;

    public boolean isPrivate() {
        return regexp!= null && regexp.isPrivate();
    }

    @Override
    public boolean matchesEmptyString() {
        return regexp != null && regexp.matchesEmptyString();
    }

    @Override
    public int getOrdinal() {
        if (regexp == null) {
            // This is the case if this RegexpRef refers to a token type
            // specified in the EXTRA_TOKENS
            int extraTokensIndex = getGrammar().getAppSettings().getExtraTokenNames().indexOf(getLabel());
            assert extraTokensIndex >=0;
            return extraTokensIndex + getGrammar().getLexerData().getRegularExpressions().size();
        }
        return regexp.getOrdinal();
    }

    @Override
    public int maximumLength() {
       return regexp.maximumLength();
    }
}

EndOfFile : "<" <_EOF> =>|| ">" ;

INJECT EndOfFile : extends org.congocc.core.RegularExpression
{
    @Override
    public boolean matchesEmptyString() {return true;}

    @Override
    public int maximumLength() {return 0;}
}

RegexpChoice :
    RegexpSequence
    ("|" RegexpSequence)*
;

RegexpChoiceInParen #RegexpChoice :
   "(" RegexpSequence ("|" RegexpSequence)* ")"
;

INJECT RegexpChoice :
    import java.util.List;
    import org.congocc.core.RegularExpression;
    extends RegularExpression;
{
    public List<RegularExpression> getChoices() {
        return childrenOfType(RegularExpression.class);
    }

    @Override
    public boolean matchesEmptyString() {
        for (RegularExpression choice: getChoices()) {
            if (choice.matchesEmptyString()) {
                return true;
            }
        }
        return false;
    }

    public int maximumLength() {
        int result = 0;
        for (RegularExpression re : getChoices()) {
            int max = re.maximumLength();
            if (max == Integer.MAX_VALUE) return Integer.MAX_VALUE;
            if (max > result) result = max;
        }
        return result;
    }
}

RegexpSequence :
   (
      RegexpStringLiteral
      |
      RegexpRef
      |
      CharacterList
      |
      RepeatedRegexp
   )+
;

INJECT RegexpSequence :
   import java.util.*;
   import org.congocc.core.RegularExpression;
   extends RegularExpression;
{

   public List<RegularExpression> getUnits() {
       return childrenOfType(RegularExpression.class);
   }

   @Override
   public boolean matchesEmptyString() {
       for (RegularExpression child : getUnits()) {
           if (!child.matchesEmptyString()) return false;
       }
       return true;
   }

    @Override
    public int maximumLength() {
        int result = 0;
        for (RegularExpression re : childrenOfType(RegularExpression.class)) {
            int max = re.maximumLength();
            if (max == Integer.MAX_VALUE) return Integer.MAX_VALUE;
            result += max;
        }
        return result;
    }
}

RepeatedRegexp #void :
{
     int r1 = 0, r2 = -1;
}
  RegexpChoiceInParen
  (  "+" #OneOrMoreRegexp(2)
   | "*" #ZeroOrMoreRegexp(2)
   | "?" #ZeroOrOneRegexp(2)
   | (
      "{" <INTEGER_LITERAL> {r1 = ((IntegerLiteral)lastConsumedToken).getValue();}
           [ "," [ <INTEGER_LITERAL> {r2=((IntegerLiteral)lastConsumedToken).getValue();} ] ]
       "}"
     ) #RepetitionRange(+1)
     {
         RepetitionRange range = (RepetitionRange) peekNode();
         range.setMin(r1);
         range.setMax(r2);
     }
  )?
;

INJECT RepetitionRange :
   import org.congocc.core.RegularExpression;
   extends RegularExpression;
{
    @Property int min = 0, max = -1;

    public boolean hasMax() {
        return firstChildOfType(COMMA) != null;
    }

    public RegularExpression getRegexp() {
        return firstChildOfType(RegularExpression.class);
    }

    @Override
    public boolean matchesEmptyString() {
        return min==0 || getRegexp().matchesEmptyString();
    }

    @Override
    public int maximumLength() {
       if (getRegexp().maximumLength() == Integer.MAX_VALUE) return Integer.MAX_VALUE;
       if (!hasMax()) {
           return min *  getRegexp().maximumLength();
       }
       return max*getRegexp().maximumLength();
    }
}

INJECT OneOrMoreRegexp :
   import org.congocc.core.RegularExpression;
   extends RegularExpression;
{
   public RegularExpression getRegexp()
   {
      return firstChildOfType(RegularExpression.class);
   }

   @Override
   public boolean matchesEmptyString() {
       return getRegexp().matchesEmptyString();
   }

   @Override
   public int maximumLength() {
      return Integer.MAX_VALUE;
   }
}

INJECT ZeroOrMoreRegexp :
   import org.congocc.core.RegularExpression;
   extends RegularExpression;
{
   public RegularExpression getRegexp()
   {
      return firstChildOfType(RegularExpression.class);
   }

   public void setRegexp(RegularExpression regexp) {
      RegularExpression current = getRegexp();
      if (current != null) {
          replace(current, regexp);
      } else {
          add(0, regexp);
      }
   }

   @Override
   public boolean matchesEmptyString() {
       return true;
   }

   @Override
   public int maximumLength() {
      return Integer.MAX_VALUE;
   }
}

INJECT ZeroOrOneRegexp :
   import org.congocc.core.RegularExpression;
   extends RegularExpression;
{
   public RegularExpression getRegexp()
   {
      return firstChildOfType(RegularExpression.class);
   }

   public void setRegexp(RegularExpression regexp) {
      RegularExpression current = getRegexp();
      if (current != null) {
          add(indexOf(current), regexp);
          remove(current);
      } else {
          add(0, regexp);
      }
   }

   @Override
   public boolean matchesEmptyString() {
       return true;
   }

   @Override
   public int maximumLength() {
       return getRegexp().maximumLength();
   }
}

CharacterList :
  ["~"]
  "[" [CharacterRange
        ( "," CharacterRange)*
      ]
  "]"
;

INJECT CharacterList :
   import java.util.*;
   import org.congocc.core.*;
   extends RegularExpression;
{
    public List<CharacterRange> getDescriptors() {
        return childrenOfType(CharacterRange.class);
    }

    public boolean isNegated() {
        return firstChildOfType(TILDE) != null;
    }

    @Override
    public boolean matchesEmptyString() {
        return false;
    }

    @Override
    public int maximumLength() {
        return 1;
    }
}

CharacterRange# :
    (<STRING_LITERAL>|<CHARACTER_LITERAL>)
    [
    "-"
    (<STRING_LITERAL>|<CHARACTER_LITERAL>)
   ]
;

INJECT CharacterRange :
  import java.util.ArrayList;
{
    @Property int left, right;

    public CharacterRange() {}

    public CharacterRange(int left, int right) {
        setRange(left, right);
    }

    public void setRange(int left, int right) {
        if (left > right) {
            throw new ArrayIndexOutOfBoundsException();
        }
        this.left = left;
        this.right = right;
    }

    public boolean isSingleChar() {
       return left == right;
    }

    static private boolean isSingleChar(String s) {
       if (s.length() == 1) return true;
       if (s.length() == 2) {
           return Character.isSurrogatePair(s.charAt(0), s.charAt(1));
       }
       return false;
    }

    public void close() {
        java.util.List<Token> bounds = new ArrayList<>();
        for (Node n : children()) {
            if (n instanceof StringLiteral) bounds.add((Token) n);
            if (n instanceof CharacterLiteral) bounds.add((Token) n);
            if (bounds.size() >1) break;
        }
        String leftBound = StringLiteral.removeEscapesAndQuotes(bounds.get(0).toString());
        String rightBound = leftBound;
        if (bounds.size() >1) {
            rightBound = StringLiteral.removeEscapesAndQuotes(bounds.get(1).toString());
        }
        if (!isSingleChar(leftBound)) {
            getErrors().addError(bounds.get(0),
                    "String in character list may contain only one character.");
        }
        this.left = this.right = leftBound.codePointAt(0);
        if (!leftBound.equals(rightBound)) {
            if (!isSingleChar(rightBound)) {
                getErrors().addError(bounds.get(1),
                        "String in character list may contain only one character.");
            }
        }
        if (bounds.size()>1) {
            String right = StringLiteral.removeEscapesAndQuotes(bounds.get(1).toString());
            if (!isSingleChar(right)) {
                getErrors().addError(bounds.get(1),
                        "String in character list may contain only one character.");
            }
            this.right = right.codePointAt(0);
        }
        if (this.left>this.right) {
            getErrors().addError(this, "The left side of the character range must be a lower ordinal (in Unicode) value than the right side.");
        }
    }
}
